BACKUP ~bubb_spell_menu_extended/backup~
AUTHOR ~Bubb~
VERSION 1.7
AUTO_TRA ~bubb_spell_menu_extended/%s~

BEGIN ~Bubb's Spell Menu Extended~
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~UI.MENU~ ~UI.MENU not found in game.~

	INCLUDE ~bubb_spell_menu_extended/bubb_lib.tph~

	ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL_CASE ~WIN32~ BEGIN
	
		ACTION_DEFINE_ASSOCIATIVE_ARRAY accepted_responses BEGIN ~1~ => 1 ~2~ => 1 END
		LAF PROMPT_USER
		STR_VAR
			to_prompt = EVAL ~This installer can attempt to patch UI.MENU instead of overriding it on Windows platforms; which installation method do you wish to perform?%WNL% 1] Patch%WNL% 2] Override~
			accepted_responses = ~accepted_responses~
		RET
			response
		END
	
	END ELSE BEGIN
	
		ACTION_DEFINE_ASSOCIATIVE_ARRAY accepted_responses BEGIN ~2~ => 1 END
		LAF PROMPT_USER
		STR_VAR
			to_prompt = EVAL ~This installer can attempt to patch UI.MENU instead of overriding it on Windows platforms; which installation method do you wish to perform?%WNL% 1] Patch (Disabled)%WNL% 2] Override~
			accepted_responses = ~accepted_responses~
		RET
			response
		END
	
	END
	
	ACTION_IF %response% == 1 BEGIN
	
		//Patch UI.MENU
		
		AT_NOW ~bubb_spell_menu_extended/work/UIUtil.exe cleanup~
		
		COPY ~bubb_spell_menu_extended\work\uipatches\0base\0base.MENU~ ~bubb_spell_menu_extended\work~
		COPY ~bubb_spell_menu_extended\work\uipatches\0base\0base.uipatch~ ~bubb_spell_menu_extended\work~
		
		ACTION_IF MOD_IS_INSTALLED ~EEUITweaks.tp2~ ~2900~ BEGIN
			COPY ~bubb_spell_menu_extended\work\uipatches\1lefreut's Improved Record Screen\1lefreut's Improved Record Screen.MENU~ ~bubb_spell_menu_extended\work~
			COPY ~bubb_spell_menu_extended\work\uipatches\1lefreut's Improved Record Screen\1lefreut's Improved Record Screen.uipatch~ ~bubb_spell_menu_extended\work~
		END
		
		COPY_EXISTING ~UI.MENU~ ~bubb_spell_menu_extended/work/to_patch.MENU~ IF_EXISTS
		AT_NOW ~bubb_spell_menu_extended/work/UIUtil.exe applypatches to_patch.MENU~
		
		ACTION_IF FILE_EXISTS ~bubb_spell_menu_extended/work/failed.mrk~ BEGIN
			FAIL ~Could not patch UI.MENU.~
		END
		
		COPY ~bubb_spell_menu_extended/work/out.MENU~ ~override/UI.MENU~
		
		//Patch UI.MENU
	
	END ELSE BEGIN
	
		COPY ~bubb_spell_menu_extended/copy/UI.MENU~ ~override/UI.MENU~
	
	END

	OUTER_SET spell_count = 0
	APPEND ~BGEE.LUA~ ~spellInfo =~
	APPEND ~BGEE.LUA~ ~{~

	COPY_EXISTING_REGEXP ~.*\.SPL~ ~override~

		LPF TO_HEX_NUMBER INT_VAR value = spell_count minDigits = 3 RET hex_uuid = hexNumber END

		READ_LONG 0x0008 unidentified_name_strref

		PATCH_IF (unidentified_name_strref != (0 - 1)) BEGIN

			GET_STRREF unidentified_name_strref unidentified_name

			LPF ARRAY_GET_VALUE INT_VAR key = unidentified_name_strref STR_VAR array_name = ~processed_strrefs~ RET already_processed = exists original_value = value END

			PATCH_IF (already_processed == 0) BEGIN

				TEXT_SPRINT unidentified_name_altered ~SPELL_UUID:%hex_uuid%~

				READ_SHORT 0x001c spell_type
				READ_LONG 0x0034 spell_level
				READ_ASCII 0x003a spell_icon

				INNER_ACTION BEGIN

					STRING_SET_EVALUATE unidentified_name_strref ~%unidentified_name_altered%~

					LAF STRING_REPLACE STR_VAR string = EVAL ~%unidentified_name%~ to_replace = ~'~ with = ~\'~ RET unidentified_name_escaped = replaced_string END

					APPEND ~BGEE.LUA~ ~%TAB%['%hex_uuid%'] =~
					APPEND ~BGEE.LUA~ ~%TAB%{~

					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['resref'] = '%SOURCE_RES%',~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['name'] = '%unidentified_name_escaped%',~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellType'] = %spell_type%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellLevel'] = %spell_level%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellIcon'] = '%spell_icon%',~

					APPEND ~BGEE.LUA~ ~%TAB%},~
				END

				TEXT_SPRINT $processed_strrefs(~%unidentified_name_strref%~) ~%unidentified_name%~

			END ELSE BEGIN

				TEXT_SPRINT unidentified_name_altered ~SPELL_UUID:%hex_uuid%~

				SET inserted_strref = RESOLVE_STR_REF (~%unidentified_name_altered%~)
				WRITE_LONG 0x0008 inserted_strref

				READ_SHORT 0x001c spell_type
				READ_LONG 0x0034 spell_level
				READ_ASCII 0x003a spell_icon

				INNER_ACTION BEGIN

					LAF STRING_REPLACE STR_VAR string = EVAL ~%original_value%~ to_replace = ~'~ with = ~\'~ RET original_value_escaped = replaced_string END

					APPEND ~BGEE.LUA~ ~%TAB%['%hex_uuid%'] =~
					APPEND ~BGEE.LUA~ ~%TAB%{~

					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['resref'] = '%SOURCE_RES%',~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['name'] = '%original_value_escaped%',~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellType'] = %spell_type%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellLevel'] = %spell_level%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellIcon'] = '%spell_icon%',~

					APPEND ~BGEE.LUA~ ~%TAB%},~
				END

				TEXT_SPRINT $processed_strrefs(~%inserted_strref%~) ~%original_value%~
			END
		END

		SET %spell_count% += 1

	BUT_ONLY_IF_IT_CHANGES

	ACTION_GET_STRREF 11555 identify_string
	ACTION_GET_STRREF 63205 jump_to_cleric
	ACTION_GET_STRREF 63206 jump_to_mage

	LAF STRING_REPLACE STR_VAR string = EVAL ~%identify_string%~ to_replace = ~'~ with = ~\'~ RET identify_string = replaced_string END
	LAF STRING_REPLACE STR_VAR string = EVAL ~%jump_to_cleric%~ to_replace = ~'~ with = ~\'~ RET jump_to_cleric = replaced_string END
	LAF STRING_REPLACE STR_VAR string = EVAL ~%jump_to_mage%~ to_replace = ~'~ with = ~\'~ RET jump_to_mage = replaced_string END

	APPEND ~BGEE.LUA~ ~%TAB%["specialTooltips"] =~
	APPEND ~BGEE.LUA~ ~%TAB%{~
	APPEND ~BGEE.LUA~ ~%TAB%%TAB%['%identify_string%'] = 1,~
	APPEND ~BGEE.LUA~ ~%TAB%%TAB%['%jump_to_cleric%'] = 1,~
	APPEND ~BGEE.LUA~ ~%TAB%%TAB%['%jump_to_mage%'] = 1,~
	APPEND ~BGEE.LUA~ ~%TAB%},~

	APPEND ~BGEE.LUA~ ~}~
	