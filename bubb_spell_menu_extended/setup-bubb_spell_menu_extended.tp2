BACKUP ~bubb_spell_menu_extended/backup~
AUTHOR ~Bubb~
VERSION 1.3
AUTO_TRA ~bubb_spell_menu_extended/%s~

BEGIN ~Bubb's Spell Menu Extended~

	/**
	 * Patch function. Converts any decimal number into a hexadecimal number.
	 * INT_VAR value      The decimal number to convert.
	 * INT_VAR minDigits  Minimum number of digits for the returned hex value. (default: 1)
	 * INT_VAR prefix     A flag that indicates whether the returned number will be prefixed with "0x". (default: 0)
	 * RET hexNumber      The converted hexadecimal number as string.
	 */
	DEFINE_PATCH_FUNCTION TO_HEX_NUMBER
	INT_VAR
	  value     = 0
	  minDigits = 1
	  prefix    = 0
	RET
	  hexNumber
	BEGIN
	  SET minDigits = (minDigits < 1) ? 1 : minDigits
	  SET minDigits = (minDigits > 8) ? 8 : minDigits
	  TEXT_SPRINT hexNumber ~~
	  PATCH_DEFINE_ARRAY digit BEGIN ~0~ ~1~ ~2~ ~3~ ~4~ ~5~ ~6~ ~7~ ~8~ ~9~ ~a~ ~b~ ~c~ ~d~ ~e~ ~f~ END

	  PATCH_IF (value < 0) BEGIN
		SET signed = 1
		SET value = 0 - value
	  END ELSE BEGIN
		SET signed = 0
	  END

	  WHILE (value != 0) BEGIN
		SET curDigit = value BAND 0xf
		SET value = value BLSR 4
		TEXT_SPRINT hexDigit $EVAL digit(~%curDigit%~)
		TEXT_SPRINT hexNumber ~%hexDigit%%hexNumber%~
	  END

	  WHILE (STRING_LENGTH ~%hexNumber%~ < minDigits) BEGIN
		TEXT_SPRINT hexNumber ~0%hexNumber%~
	  END

	  PATCH_IF (prefix) BEGIN
		TEXT_SPRINT hexNumber ~0x%hexNumber%~
	  END

	  PATCH_IF (signed) BEGIN
		TEXT_SPRINT hexNumber ~-%hexNumber%~
	  END
	END

	// Action version of TO_HEX_NUMBER
	DEFINE_ACTION_FUNCTION TO_HEX_NUMBER
	INT_VAR
	  value     = 0
	  minDigits = 1
	  prefix    = 0
	RET
	  hexNumber
	BEGIN
	  OUTER_PATCH ~~ BEGIN
		LPF TO_HEX_NUMBER INT_VAR value = value minDigits = minDigits prefix = prefix RET hexNumber END
	  END
	END

	/**
	 * Patch function. Checks if an array key exists, and returns the value associated with said key. 
	 * INT_VAR key         The key to check for.
	 * STR_VAR array_name  The name of the array to check.
	 * RET result          1 if array contains key, 0 if array doesn't contain key.
	 */
	DEFINE_PATCH_FUNCTION ARRAY_GET_VALUE
	INT_VAR
		key = 0
	STR_VAR
		array_name = ~~
	RET
		exists
		value
	BEGIN
		PATCH_IF VARIABLE_IS_SET $~%array_name%~(~%key%~) BEGIN
			exists = 1
			PATCH_IF IS_AN_INT $~%array_name%~(~%key%~) BEGIN
				SET value = $~%array_name%~(~%key%~)
			END ELSE BEGIN
				TEXT_SPRINT value $~%array_name%~(~%key%~)
			END
		END ELSE BEGIN
			exists = 0
			value = 0
		END
	END
	
	// Action version of ARRAY_GET_VALUE
	DEFINE_ACTION_FUNCTION ARRAY_GET_VALUE
	INT_VAR
		key   = 0
	STR_VAR
		array_name = ~~
	RET
		exists
		value
	BEGIN
		OUTER_PATCH ~~ BEGIN
			LPF ARRAY_GET_VALUE INT_VAR key = key STR_VAR array_name = EVAL ~%array_name%~ RET exists value END
		END
	END
	
	// STRING_REPLACE
	DEFINE_PATCH_FUNCTION STRING_REPLACE
	STR_VAR
		string = ~~
		to_replace = ~~
		with = ~~
	RET
		replaced_string
	BEGIN

		TEXT_SPRINT replaced_string ~~
		SET to_replace_length = STRING_LENGTH ~%to_replace%~
		SET last_found_index = (0 - to_replace_length)
		SET found_index = INDEX (EXACT_MATCH ~%to_replace%~ ~%string%~)
		
		WHILE (found_index != (0 - 1)) BEGIN
		
			PATCH_IF (found_index > 0) BEGIN
			
				SET before_substring_start = last_found_index + to_replace_length
				SET before_substring_length = found_index - last_found_index - to_replace_length
				LPF SUBSTRING INT_VAR start = before_substring_start length = before_substring_length STR_VAR string = EVAL ~%string%~ RET before_substring = substring END
				TEXT_SPRINT replaced_string ~%replaced_string%%before_substring%~
			END
		
			TEXT_SPRINT replaced_string ~%replaced_string%%with%~
			SET last_found_index = found_index
			SET found_index = INDEX (EXACT_MATCH ~%to_replace%~ ~%string%~ found_index + 1)
		END
	
		SET string_length = STRING_LENGTH ~%string%~
		SET after_substring_start = last_found_index + to_replace_length
		SET after_substring_length = string_length - last_found_index - to_replace_length
		LPF SUBSTRING INT_VAR start = after_substring_start length = after_substring_length STR_VAR string = EVAL ~%string%~ RET after_substring = substring END
		TEXT_SPRINT replaced_string ~%replaced_string%%after_substring%~
	END
	
	// Action version of STRING_REPLACE
	DEFINE_ACTION_FUNCTION STRING_REPLACE
	STR_VAR
		string = ~~
		to_replace = ~~
		with = ~~
	RET
		replaced_string
	BEGIN
		OUTER_PATCH ~~ BEGIN
			LPF STRING_REPLACE STR_VAR string = EVAL ~%string%~ to_replace = EVAL ~%to_replace%~ with = EVAL ~%with%~ RET replaced_string END
		END
	END
	
	
	OUTER_SET spell_count = 0
	APPEND ~BGEE.LUA~ ~spellInfo =~
	APPEND ~BGEE.LUA~ ~{~
	
	COPY_EXISTING_REGEXP ~.*\.SPL~ ~override~
	
		//PATCH_PRINT ~Processing %SOURCE_FILE%~
	
		LPF TO_HEX_NUMBER
		INT_VAR
			value = spell_count
			minDigits = 3
		RET
			hex_uuid = hexNumber
		END
		
		//PATCH_PRINT ~	Spell Hex UUID: %hex_uuid%~

		READ_LONG 0x0008 unidentified_name_strref
		//PATCH_PRINT ~	Spell Strref: %unidentified_name_strref%~
	
		PATCH_IF (unidentified_name_strref != (0 - 1)) BEGIN
		
			GET_STRREF unidentified_name_strref unidentified_name
			//PATCH_PRINT ~	Spell Name: %unidentified_name%~
		
			LPF ARRAY_GET_VALUE
			INT_VAR
				key = unidentified_name_strref
			STR_VAR
				array_name = ~processed_strrefs~
			RET
				already_processed = exists
				original_value = value
			END
		
			PATCH_IF (already_processed == 0) BEGIN
			
				TEXT_SPRINT unidentified_name_altered ~SPELL_UUID:%hex_uuid%~
				
				//PATCH_PRINT ~	Spell Altered Name: %unidentified_name_altered%~
				
				READ_SHORT 0x001c spell_type
				READ_LONG 0x0034 spell_level
				READ_ASCII 0x003a spell_icon
				
				INNER_ACTION BEGIN

					STRING_SET_EVALUATE unidentified_name_strref ~%unidentified_name_altered%~
					
					LAF STRING_REPLACE
					STR_VAR
						string = EVAL ~%unidentified_name%~
						to_replace = ~'~ with = ~\'~
					RET
						unidentified_name_escaped = replaced_string
					END
					
					APPEND ~BGEE.LUA~ ~%TAB%['%hex_uuid%'] =~
					APPEND ~BGEE.LUA~ ~%TAB%{~
					
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['name'] = '%unidentified_name_escaped%',~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellType'] = %spell_type%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellLevel'] = %spell_level%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellIcon'] = '%spell_icon%',~
					
					APPEND ~BGEE.LUA~ ~%TAB%},~
				END
			
				TEXT_SPRINT $processed_strrefs(~%unidentified_name_strref%~) ~%unidentified_name%~
			
			END ELSE BEGIN
			
				//PATCH_PRINT ~	Already Processed %unidentified_name_strref%, Original Value: %original_value%~
				
				TEXT_SPRINT unidentified_name_altered ~SPELL_UUID:%hex_uuid%~
				
				//PATCH_PRINT ~	Spell Altered Name: %unidentified_name_altered%~
				
				SET inserted_strref = RESOLVE_STR_REF (~%unidentified_name_altered%~)
				WRITE_LONG 0x0008 inserted_strref
				
				READ_SHORT 0x001c spell_type
				READ_LONG 0x0034 spell_level
				READ_ASCII 0x003a spell_icon
				
				INNER_ACTION BEGIN
				
					STRING_SET_EVALUATE unidentified_name_strref ~%unidentified_name_altered%~
					
					LAF STRING_REPLACE
					STR_VAR 
						string = EVAL ~%original_value%~
						to_replace = ~'~ with = ~\'~
					RET
						original_value_escaped = replaced_string
					END
					
					APPEND ~BGEE.LUA~ ~%TAB%['%hex_uuid%'] =~
					APPEND ~BGEE.LUA~ ~%TAB%{~
					
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['name'] = '%original_value_escaped%',~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellType'] = %spell_type%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellLevel'] = %spell_level%,~
					APPEND ~BGEE.LUA~ ~%TAB%%TAB%['spellIcon'] = '%spell_icon%',~
	
					APPEND ~BGEE.LUA~ ~%TAB%},~
				END
				
				TEXT_SPRINT $processed_strrefs(~%inserted_strref%~) ~%original_value%~
			END
		END
		
		SET %spell_count% += 1
		
	BUT_ONLY_IF_IT_CHANGES

	ACTION_GET_STRREF 11555 identify_string
	ACTION_GET_STRREF 63205 jump_to_cleric
	ACTION_GET_STRREF 63206 jump_to_mage
	
	LAF STRING_REPLACE STR_VAR string = EVAL ~%identify_string%~ to_replace = ~'~ with = ~\'~ RET identify_string = replaced_string END
	LAF STRING_REPLACE STR_VAR string = EVAL ~%jump_to_cleric%~ to_replace = ~'~ with = ~\'~ RET jump_to_cleric = replaced_string END
	LAF STRING_REPLACE STR_VAR string = EVAL ~%jump_to_mage%~ to_replace = ~'~ with = ~\'~ RET jump_to_mage = replaced_string END
	
	APPEND ~BGEE.LUA~ ~%TAB%["specialTooltips"] =~
	APPEND ~BGEE.LUA~ ~%TAB%{~
	APPEND ~BGEE.LUA~ ~%TAB%%TAB%['%identify_string%'] = 1,~
	APPEND ~BGEE.LUA~ ~%TAB%%TAB%['%jump_to_cleric%'] = 1,~
	APPEND ~BGEE.LUA~ ~%TAB%%TAB%['%jump_to_mage%'] = 1,~
	APPEND ~BGEE.LUA~ ~%TAB%},~
	
	APPEND ~BGEE.LUA~ ~}~
	//PRINT ~There were %spell_count% spells.~
	